% Nama : Ghifa Attaya Ulhaq
%% Kelas: Matematika B
%% NIM  : 22305144038
% 
% * Menggambar Plot 3D dengan EMT
% 
% Ini adalah pengenalan plot 3D di Euler. Kita memerlukan plot 3D untuk
% memvisualisasikan fungsi dua variabel.
% 
% Euler menggambar fungsi tersebut menggunakan algoritma pengurutan
% untuk menyembunyikan bagian di latar belakang. Secara umum Euler
% menggunakan proyeksi sentral. Defaultnya adalah dari kuadran x-y
% positif menuju titik asal x=y=z=0, tetapi sudut=0° dilihat dari arah
% sumbu y. Sudut pandang dan ketinggian dapat diubah.
% 
% Euler bisa merencanakan
% 
% - permukaan dengan garis penetasan dan level atau rentang level,
%% - awan titik,
%% - kurva parametrik,
%% - permukaan implisit.
% 
% Plot 3D suatu fungsi menggunakan plot3d. Cara termudah adalah dengan
% memplot ekspresi dalam x dan y. Parameter r mengatur rentang plot
% sekitar (0,0).
>aspect(1.5); plot3d("x^2+sin(y)",-5,5,0,6*pi):
 %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-001.png
>plot3d("x^2+x*sin(y)",-5,5,0,6*pi):
 %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-002.png
% Silakan lakukan modifikasi agar gambar "talang bergelombang" tersebut tidak lurus melainkan melengkung/melingkar, baik
% melingkar secara mendatar maupun melingkar turun/naik (seperti papan peluncur pada kolam renang. Temukan rumusnya.
>
% * Fungsi dari dua Variabel
% 
% Untuk grafik fungsi, gunakan
% 
% - ekspresi sederhana dalam x dan y,
%% - nama fungsi dari dua variabel
%% - atau matriks data.
% 
% Default dari fungsi ini adalah jaringan kawat yang diisi dengan warna
% yang berbeda di kedua sisi. Perhatikan bahwa jumlah default interval
% grid adalah 10, tapi plot menggunakan nomor default persegi panjang
% 40x40 untuk membangun permukaan. Tetapi, tetap dapat diubah.
% 
% - n = 40, n =[40,40]: jumlah garis grid di setiap arah
%% - grid = 10, grid =[10,10]: jumlah garis grid di setiap arah.
% 
% Kita menggunakan default n = 40 dan grid = 10.
>plot3d("x^2+y^2"):
 %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-003.png
% Interaksi pengguna dimungkinkan dengan parameter pengguna. Pengguna
% dapat menekan kunci berikut.
% 
% - kiri, kanan, atas, bawah: putar sudut pandang
%% - +,-: memperbesar atau keluar
%% - a: menghasilkan anaglyph (lihat di bawah)
%% - l: beralih memutar sumber cahaya (lihat di bawah)
%% - space: reset ke default
%% - return: interaksi akhir
>plot3d("exp(-x^2+y^2)",>user, ...
>  title="Turn with the vector keys (press return to finish)"):
 %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-004.png
% Rentang plot untuk fungsi dapat ditentukan dengan
% 
% - a, b: jarak x
%% - c, d: Jarak Y
%% - r: alun-alun simetris sekitar (0,0).
%% - n: jumlah subinterval untuk plot.
% 
% Ada beberapa parameter untuk meningkatkan fungsi atau mengubah
% tampilan grafik.
% 
% fscale: skala ke nilai fungsi (default adalah <fscale).
%% scale: nomor atau vektor 1x2 untuk skala menjadi x- dan arah-y.
%% frame: jenis bingkai (default 1).
>plot3d("exp(-(x^2+y^2)/5)",r=10,n=80,fscale=4,scale=1.2,frame=3,>user):
 %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-005.png
% Pandangannya bisa diubah dengan berbagai cara.
% 
% - distance: jarak pandang ke plot.
%% - zoom: nilai zoom.
%% - angel: sudut ke sumbu-y negatif dalam radian.
%% - height: tinggi pemandangan dalam radian.
% 
% Nilai default dapat diperiksa atau diubah dengan fungsi view(). Ini
% mengembalikan parameter dalam urutan di atas.
>view
[5,  2.6,  2,  0.4]
% Jarak yang lebih dekat membutuhkan zoom yang lebih sedikit. Efeknya
% lebih seperti lensa sudut lebar.
% 
% Dalam contoh berikut, angle= 0 dan height= 0 terlihat dari sumbu y
% negatif. Label sumbu untuk y tersembunyi dalam kasus ini.
> plot3d("x^2+y",distance=3,zoom=1,angle=pi/2,height=0):
 %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-006.png
% Plotnya selalu terlihat di tengah kubus plot. Anda dapat memindahkan
% pusatnya dengan parameter tengah.
>plot3d("x^4+y^2",a=0,b=1,c=-1,d=1,angle=-20°,height=20°, ...
>  center=[0.4,0,0],zoom=5):
 %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-007.png
% Plotnya ditingkatkan agar masuk ke dalam kubus unit untuk dilihat.
% Jadi tidak perlu mengubah jarak atau zoom tergantung pada ukuran plot.
% Labelnya mengacu pada ukuran sebenarnya.
% 
% Jika Anda mematikan ini dengan scale=false, Anda perlu berhati-hati,
% bahwa plot masih cocok ke jendela plot, dengan mengubah jarak pandang
% atau zoom, dan memindahkan pusat.
>plot3d("5*exp(-x^2-y^2)",r=2,<fscale,<scale,distance=13,height=50°, ...
>  center=[0,0,-2],frame=3):
 %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-008.png
% Plot kutub juga tersedia. Parameter polar=true menarik plot kutub.
% Fungsi tetap harus menjadi fungsi x dan y. Parameter "fscale" skala
% fungsinya dengan skala sendiri. Jika tidak, fungsi ini ditingkatkan
% agar masuk ke dalam kubus.
>plot3d("1/(x^2+y^2+1)",r=5,>polar, ...
>fscale=2,>hue,n=100,zoom=4,>contour,color=blue):
 %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-009.png
>function f(r) := exp(-r/2)*cos(r); ...
>plot3d("f(x^2+y^2)",>polar,scale=[1,1,0.4],r=pi,frame=3,zoom=4):
 %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-010.png
% Parameter berputar fungsi dalam x di sekitar sumbu x.
% 
% - rotate=1: Gunakan sumbu x
%% - rotate=2: Gunakan sumbu z
>plot3d("x^2+1",a=-1,b=1,rotate=true,grid=5):
 %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-011.png
>plot3d("x^2+1",a=-1,b=1,rotate=2,grid=5):
 %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-012.png
>plot3d("sqrt(25-x^2)",a=0,b=5,rotate=1):
 %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-013.png
>
>plot3d("x*sin(x)",a=0,b=6pi,rotate=2):
 %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-014.png
% Berikut adalah plot dengan tiga fungsi.
>plot3d("x","x^2+y^2","y",r=2,zoom=3.5,frame=3):
 %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-015.png
% * Plot kontur
% 
% Untuk plotnya, Euler menambahkan garis grid. Sebaliknya, mungkin untuk
% menggunakan garis level dan rona satu warna atau rona berwarna
% spektral. Euler dapat menggambar ketinggian fungsi pada plot dengan
% bayangan. Dalam semua plot 3D Euler dapat menghasilkan anaglyph
% merah/cyan.
% 
% - >hue: Mengaktifkan bayangan cahaya, bukan kabel.
%% - >contour: Plot garis kontur otomatis pada plot.
%% - level=... (atau levels): Sebuah vektor nilai untuk garis kontur.
% 
% Defaultnya adalah level="auto", yang menghitung beberapa garis level
% secara otomatis. Seperti yang kau lihat di plot, tingkat sebenarnya
% adalah tingkat tingkat.
% 
% Gaya default bisa diubah. Untuk plot kontur berikut, kita menggunakan
% grid yang lebih halus untuk 100x100 poin, skala fungsi dan plot, dan
% menggunakan sudut pandang yang berbeda.
>plot3d("exp(-x^2-y^2)",r=2,n=100,level="thin", ...
> >contour,>spectral,fscale=1,scale=1.1,angle=45°,height=20°):
 %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-016.png
>plot3d("exp(x*y)",angle=100°,>contour,color=green):
 %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-017.png
% Shading default menggunakan warna abu-abu. Tapi berbagai warna
% spektral juga tersedia.
% 
% - >spectral: Digunakan skema spektral default
%% - color=...: Menggunakan warna khusus atau skema spektral
% 
% Untuk plot berikut, kita menggunakan skema spektral default dan
% meningkatkan jumlah poin untuk mendapatkan tampilan yang sangat halus.
>plot3d("x^2+y^2",>spectral,>contour,n=100):
 %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-018.png
% Alih-alih garis tingkat otomatis, kita juga dapat menetapkan nilai
% garis level. Ini akan menghasilkan garis tingkat tipis bukannya
% rentang level.
>plot3d("x^2-y^2",0,5,0,5,level=-1:0.1:1,color=redgreen):
 %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-019.png
% Dalam plot berikut, kita menggunakan dua band tingkat yang sangat luas
% dari -0.1 ke 1, dan dari 0.9 ke 1. Ini dimasukkan sebagai matriks
% dengan batas tingkat sebagai kolom.
% 
% Selain itu, kita melapisi grid dengan 10 interval ke setiap arah.
>plot3d("x^2+y^3",level=[-0.1,0.9;0,1], ...
>  >spectral,angle=30°,grid=10,contourcolor=gray):
 %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-020.png
% Dalam contoh berikut, kita merencanakan set, di mana
% 
% latex: f(x,y) = x^y-y^x = 0
% %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-021.png
% // latex: f(x,y) = x^y-y^x = 0
% 
% Kita menggunakan garis tipis tunggal untuk garis level.
>plot3d("x^y-y^x",level=0,a=0,b=6,c=0,d=6,contourcolor=red,n=100):
 %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-022.png
% Hal ini dimungkinkan untuk menunjukkan pesawat kontur di bawah plot.
% Warna dan jarak ke plot dapat ditentukan.
>plot3d("x^2+y^4",>cp,cpcolor=green,cpdelta=0.2):
 %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-023.png
% Berikut adalah beberapa gaya lagi. Kita selalu mematikan bingkai, dan
% menggunakan berbagai skema warna untuk plot dan grid.
>figure(2,2); ...
>expr="y^3-x^2"; ...
>figure(1);  ...
>  plot3d(expr,<frame,>cp,cpcolor=spectral); ...
>figure(2);  ...
>  plot3d(expr,<frame,>spectral,grid=10,cp=2); ...
>figure(3);  ...
>  plot3d(expr,<frame,>contour,color=gray,nc=5,cp=3,cpcolor=greenred); ...
>figure(4);  ...
>  plot3d(expr,<frame,>hue,grid=10,>transparent,>cp,cpcolor=gray); ...
>figure(0):
 %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-024.png
% Ada beberapa skema spektral lainnya, bernomor dari 1 sampai 9. Tetapi
% Anda juga bisa menggunakan color=value, dimana nilai
% 
% - spectral: untuk jangkauan dari biru ke merah
%% - white: untuk rentang yang lebih redup
%% - yellowblue,purplegreen,blueyellow,greenred
%% - blueyellow, greenpurple,yellowblue,redgreen
>figure(3,3); ...
>for i=1:9;  ...
>  figure(i); plot3d("x^2+y^2",spectral=i,>contour,>cp,<frame,zoom=4);  ...
>end; ...
>figure(0):
 %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-025.png
% Sumber cahaya dapat diubah dengan l dan kunci kursor selama interaksi
% pengguna. Hal ini juga dapat diatur dengan parameter.
% 
% - light: arah untuk cahaya
%% - amb: cahaya ambient antara 0 dan 1
% 
% Perhatikan bahwa program tidak membuat perbedaan antara sisi plot.
% Tidak ada bayangan. Untuk ini Anda akan membutuhkan Povray.
>plot3d("-x^2-y^2", ...
>  hue=true,light=[0,1,1],amb=0,user=true, ...
>  title="Press l and cursor keys (return to exit)"):
 %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-026.png
% Parameter warna mengubah warna permukaan. Warna garis level juga bisa
% diubah.
>plot3d("-x^2-y^2",color=rgb(0.2,0.2,0),hue=true,frame=false, ...
>  zoom=3,contourcolor=red,level=-2:0.1:1,dl=0.01):
 %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-027.png
% Warna 0 memberikan efek pelangi khusus.
>plot3d("x^2/(x^2+y^2+1)",color=0,hue=true,grid=10):
 %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-028.png
% Permukaannya juga bisa transparan.
>plot3d("x^2+y^2",>transparent,grid=10,wirecolor=red):
 %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-029.png
% * Plot implisit
% 
% Pada tiga dimensi, juga terdapat plot implisit. Euler menghasilkan
% potongan melalui benda. Fitur plot3d termasuk plot implisit. Plot ini
% menunjukkan nol set fungsi dalam tiga variabel.
%% Solusi dari
% 
% latex: f(x,y,z) = 0
% %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-030.png
% // latex: f(x,y,z) = 0
% 
% dapat divisualisasikan dalam potongan paralel dengan x-y-, x-z- dan
% bidang y-z.
% 
% - implicit=1: potong paralel dengan bidang y-z
%% - implicit=2: potong paralel dengan bidang x-z
%% - implicit=4: potong paralel dengan bidang x-y
% 
% Tambahkan nilai-nilai ini, jika Anda suka. Dalam contoh kita
% merencanakan
% 
% latex: M = \{ (x,y,z) : x^2+y^3+zy=1 \}
% %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-031.png
% // latex: M = \{ (x,y,z) : x^2+y^3+zy=1 \}
>plot3d("x^2+y^3+z*y-1",r=5,implicit=3):
 %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-032.png
>c=1; d=1;
>plot3d("((x^2+y^2-c^2)^2+(z^2-1)^2)*((y^2+z^2-c^2)^2+(x^2-1)^2)*((z^2+x^2-c^2)^2+(y^2-1)^2)-d",r=2,<frame,>implicit,>user): 
 %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-033.png
>
>plot3d("x^2+y^2+4*x*z+z^3",>implicit,r=2,zoom=2.5):
 %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-034.png
% * Merencanakan Data 3D
% 
% Sama seperti plot2d, plot3d menerima data. Untuk objek 3D, Anda perlu
% menyediakan matriks nilai dari x-, y- dan z-, atau tiga fungsi atau
% ekspresi fx (x, y), fy (x, y), fz(x, y).
% 
% latex: \gamma(t,s) = (x(t,s),y(t,s),z(t,s))
% %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-035.png
% // latex: \gamma(t,s) = (x(t,s),y(t,s),z(t,s))
% 
% Karena x,y,z adalah matriks, kita berasumsi bahwa (t, s) berjalan
% melalui grid persegi. Akibatnya, Anda dapat merencanakan gambar
% persegi panjang di ruang angkasa.
% 
% Anda bisa menggunakan bahasa matriks Euler untuk menghasilkan
% koordinat secara efektif.
% 
% Dalam contoh berikut, kita menggunakan vektor nilai t dan vektor kolom
% nilai s untuk parameter permukaan bola. Dalam gambar, kita bisa
% menandai daerah, dalam kasus kita wilayah kutub.
>t=linspace(0,2pi,180); s=linspace(-pi/2,pi/2,90)'; ...
>x=cos(s)*cos(t); y=cos(s)*sin(t); z=sin(s); ...
>plot3d(x,y,z,>hue, ...
>color=blue,<frame,grid=[10,20], ...
>values=s,contourcolor=red,level=[90°-24°;90°-22°], ...
>scale=1.4,height=50°):
 %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-036.png
% Berikut adalah contoh, yang merupakan grafik fungsi.
>t=-1:0.1:1; s=(-1:0.1:1)'; plot3d(t,s,t*s,grid=10):
 %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-037.png
% Namun, kita bisa membuat segala macam permukaan. Berikut adalah
% permukaan yang sama dengan fungsi
% 
% latex: x = y \, z
% %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-038.png
% // latex: x = y \, z
>plot3d(t*s,t,s,angle=180°,grid=10):
 %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-039.png
% Dengan lebih banyak usaha, kita bisa menghasilkan banyak permukaan.
% 
% Dalam contoh berikut kita membuat pandangan teduh dari bola
% terdistorsi. Koordinat biasa untuk bola adalah
% 
% latex: \gamma(t,s) = (\cos(t)\cos(s),\sin(t)\sin(s),\cos(s))
% %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-040.png
% // latex: \gamma(t,s) = (\cos(t)\cos(s),\sin(t)\sin(s),\cos(s))
% 
% dengan
% 
% latex: 0 \le t \le 2\pi, \quad \frac{-\pi}{2} \le s \le \frac{\pi}{2}.
% %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-041.png
% // latex: 0 \le t \le 2\pi, \quad \frac{-\pi}{2} \le s \le \frac{\pi}{2}.
% 
% Kami mendistorsi ini dengan faktor
% 
% latex: d(t,s) = \frac{\cos(4t)+\cos(8s)}{4}.
% %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-042.png
% // latex: d(t,s) = \frac{\cos(4t)+\cos(8s)}{4}.
>t=linspace(0,2pi,320); s=linspace(-pi/2,pi/2,160)'; ...
>d=1+0.2*(cos(4*t)+cos(8*s)); ...
>plot3d(cos(t)*cos(s)*d,sin(t)*cos(s)*d,sin(s)*d,hue=1, ...
>  light=[1,0,1],frame=0,zoom=5):
 %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-043.png
% Tentu saja, point cloud juga mungkin. Untuk merencanakan data titik di
% ruang, kita perlu tiga vektor untuk koordinat titik.
% 
% Gaya yang digunakan sama seperti di plot2d dengan points=true;
>n=500;  ...
>  plot3d(normal(1,n),normal(1,n),normal(1,n),points=true,style="."):
 %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-044.png
% Hal ini juga mungkin untuk merencanakan kurva dalam 3D. Dalam hal ini,
% lebih mudah untuk menghitung kembali titik kurva. Untuk kurva di
% bidang kita menggunakan urutan koordinat dan parameter wire=true.
>t=linspace(0,8pi,500); ...
>plot3d(sin(t),cos(t),t/10,>wire,zoom=3):
 %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-045.png
>t=linspace(0,4pi,1000); plot3d(cos(t),sin(t),t/2pi,>wire, ...
>linewidth=3,wirecolor=blue):
 %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-046.png
>X=cumsum(normal(3,100)); ...
> plot3d(X[1],X[2],X[3],>anaglyph,>wire):
 %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-047.png
% EMT juga bisa plot dalam mode anaglyph. Untuk melihat plot seperti
% itu, Anda perlu kacamata merah/cyan.
> plot3d("x^2+y^3",>anaglyph,>contour,angle=30°):
 %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-048.png
% Seringkali, skema warna spektral digunakan untuk plot. Ini menekankan
% ketinggian fungsi.
>plot3d("x^2*y^3-y",>spectral,>contour,zoom=3.2):
 %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-049.png
% Euler juga bisa merencanakan permukaan parameter, ketika parameternya
% adalah nilai x-, y-, dan z dari gambar grid persegi panjang di ruang.
% 
% Untuk demo berikut, kita mengatur parameter u- dan v-, dan
% menghasilkan koordinat ruang dari ini.
>u=linspace(-1,1,10); v=linspace(0,2*pi,50)'; ...
>X=(3+u*cos(v/2))*cos(v); Y=(3+u*cos(v/2))*sin(v); Z=u*sin(v/2); ...
>plot3d(X,Y,Z,>anaglyph,<frame,>wire,scale=2.3):
 %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-050.png
% Berikut adalah contoh yang lebih rumit, yang megah dengan kacamata
% merah/cyan.
>u:=linspace(-pi,pi,160); v:=linspace(-pi,pi,400)';  ...
>x:=(4*(1+.25*sin(3*v))+cos(u))*cos(2*v); ...
>y:=(4*(1+.25*sin(3*v))+cos(u))*sin(2*v); ...
> z=sin(u)+2*cos(3*v); ...
>plot3d(x,y,z,frame=0,scale=1.5,hue=1,light=[1,0,-1],zoom=2.8,>anaglyph):
 %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-051.png
% * Plot Statistik
% 
% Pada plot statistik, digunakan plot bar. Plot bar adalah jenis plot
% statistik yang digunakan untuk menampilkan data kategori atau data
% diskrit. Untuk ini, kita harus menyediakan
% 
% - x: vektor baris dengan elemen n + 1
%% - y: vektor kolom dengan elemen n + 1
%% - Z: matriks nxn dari nilai-nilai.
% 
% z bisa lebih besar, tapi hanya nilai nxn yang akan digunakan.
% 
% Dalam contoh, kita pertama menghitung nilai-nilai. Kemudian kita
% menyesuaikan x dan y, sehingga pusat vektor pada nilai yang digunakan.
>x=-1:0.1:1; y=x'; z=x^2+y^2; ...
>xa=(x|1.1)-0.05; ya=(y_1.1)-0.05; ...
>plot3d(xa,ya,z,bar=true):
 %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-052.png
% Hal ini dimungkinkan untuk membagi plot permukaan dalam dua atau lebih
% bagian.
>x=-1:0.1:1; y=x'; z=x+y; d=zeros(size(x)); ...
>plot3d(x,y,z,disconnect=2:2:20):
 %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-053.png
% Jika memuat atau menghasilkan matriks data M dari file dan perlu plot
% dalam 3D Anda dapat baik skala matriks ke [-1,1] dengan scale(M), atau
% skala matriks dengan >zscale. Ini dapat dikombinasikan dengan faktor
% penskalaan individu yang diterapkan juga.
>i=1:20; j=i'; ...
>plot3d(i*j^2+100*normal(20,20),>zscale,scale=[1,1,1.5],angle=-40°,zoom=1.8):
 %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-054.png
>Z=intrandom(5,100,6); v=zeros(5,6); ...
>loop 1 to 5; v[#]=getmultiplicities(1:6,Z[#]); end; ...
>columnsplot3d(v',scols=1:5,ccols=[1:5]):
 %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-055.png
% * Permukaan Benda Putar
>plot2d("(x^2+y^2-1)^3-x^2*y^3",r=1.3, ...
>style="#",color=red,<outline, ...
>level=[-2;0],n=100):
 %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-056.png
>ekspresi &= (x^2+y^2-1)^3-x^2*y^3; $ekspresi
 %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-057.png
% // latex: \left(y^2+x^2-1\right)^3-x^2\,y^3
% Kita ingin mengubah kurva jantung di sekitar sumbu y. Berikut adalah
% ekspresi, yang mendefinisikan jantung:
% 
% latex: f(x,y)=(x^2+y^2-1)^3-x^2.y^3.
% %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-058.png
% // latex: f(x,y)=(x^2+y^2-1)^3-x^2.y^3.
% 
% Selanjutnya kita mengatur
% 
% latex: x=r.cos(a),\quad y=r.sin(a).
% %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-059.png
% // latex: x=r.cos(a),\quad y=r.sin(a).
>function fr(r,a) &= ekspresi with [x=r*cos(a),y=r*sin(a)] | trigreduce; $fr(r,a)
 %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-060.png
% // latex: \left(r^2-1\right)^3+\frac{\left(\sin \left(5\,a\right)-\sin \left(  3\,a\right)-2\,\sin a\right)\,r^5}{16}
% Hal ini memungkinkan untuk menentukan fungsi numerik, yang memecahkan
% untuk r, jika diberikan. Dengan fungsi itu kita bisa merencanakan
% jantung berubah sebagai permukaan parametrik.
>function map f(a) := bisect("fr",0,2;a); ...
>t=linspace(-pi/2,pi/2,100); r=f(t);  ...
>s=linspace(pi,2pi,100)'; ...
>plot3d(r*cos(t)*sin(s),r*cos(t)*cos(s),r*sin(t), ...
>>hue,<frame,color=red,zoom=4,amb=0,max=0.7,grid=12,height=50°):
 %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-061.png
% Berikut ini adalah plot 3D dari gambar di atas diputar di sekitar
% sumbu z. Kita mendefinisikan fungsi, yang menggambarkan objek.
>function f(x,y,z) ...
$r=x^2+y^2;
$return (r+z^2-1)^3-r*z^3;
$ endfunction
>plot3d("f(x,y,z)", ...
>xmin=0,xmax=1.2,ymin=-1.2,ymax=1.2,zmin=-1.2,zmax=1.4, ...
>implicit=1,angle=-30°,zoom=2.5,n=[10,100,60],>anaglyph):
 %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-062.png
% * Plot 3D khusus
% 
% Fungsi plot3d bagus untuk dimiliki, tapi tidak memenuhi semua
% kebutuhan. Selain rutinitas dasar, adalah mungkin untuk mendapatkan
% plot berbingkai dari objek yang Anda suka.
% 
% Meskipun Euler bukan program 3D, itu dapat menggabungkan beberapa
% objek dasar. Kita akan mencoba memvisualisasikan paraboloid dan
% singgung nya.
>function myplot ...
$  y=-1:0.01:1; x=(-1:0.01:1)';
$  plot3d(x,y,0.2*(x-0.1)/2,<scale,<frame,>hue, ..
$    hues=0.5,>contour,color=orange);
$  h=holding(1);
$  plot3d(x,y,(x^2+y^2)/2,<scale,<frame,>contour,>hue);
$  holding(h);
$endfunction
% Sekarang framedplot() menyediakan bingkai, dan mengatur pemandangan.
>framedplot("myplot",[-1,1,-1,1,0,1],height=0,angle=-30°, ...
>  center=[0,0,-0.7],zoom=3):
 %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-063.png
% Dengan cara yang sama, Anda dapat merencanakan bidang kontur secara
% manual. Perhatikan bahwa plot3d() mengatur jendela ke fullwindow()
% secara default, tapi plotcontourplane() mengasumsikan itu.
>x=-1:0.02:1.1; y=x'; z=x^2-y^4;
>function myplot (x,y,z) ...
$  zoom(2);
$  wi=fullwindow();
$  plotcontourplane(x,y,z,level="auto",<scale);
$  plot3d(x,y,z,>hue,<scale,>add,color=white,level="thin");
$  window(wi);
$  reset();
$endfunction
>myplot(x,y,z):
 %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-064.png
% * Animasi
% 
% Euler dapat menggunakan bingkai untuk menghitung animasi.
% 
% Salah satu fungsi, yang menggunakan teknik ini adalah berputar. Ini
% dapat mengubah sudut pandang dan menggambar ulang plot 3D. Fungsi ini
% memanggil addpage() untuk setiap plot baru. Akhirnya itu menghidupkan
% plotnya.
% 
% Silakan mempelajari sumber putaran (the source of rotate) untuk
% melihat lebih detail.
>function testplot () := plot3d("x^2+y^3"); ...
>rotate("testplot"); testplot():
 %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-065.png
% * Menggambar Povray
% 
% Dengan bantuan file Euler povray.e, Euler dapat menghasilkan file
% Povray. Hasilnya sangat bagus untuk dilihat.
% 
% Anda perlu menginstal Povray (32bit atau 64bit) dari
% http://www.povray.org/ , dan menempatkan sub-direktori "bin" Povray ke jalur lingkungan, atau mengatur variabel "defaultpovray" dengan jalur penuh menunjuk ke "pvengine.exe".
% 
% Antarmuka Povray dari Euler menghasilkan file Povray di direktori
% rumah pengguna, dan memanggil Povray untuk mengurai file-file ini.
% Nama file default adalah current.pov, dan direktori default adalah
% eulerhome(), biasanya c:\User\User\Euler. Povray menghasilkan file
% PNG, yang dapat dimuat oleh Euler ke dalam buku catatan. Untuk
% membersihkan file-file ini, gunakan povclear().
% 
% Fungsi pov3d memiliki dukungan yang sama dengan plot3d. Ini dapat
% menghasilkan grafik fungsi f(x,y), atau permukaan dengan koordinat X,
% Y, Z dalam matriks, termasuk garis tingkat opsional. Fungsi ini
% memulai raytracer secara otomatis, dan memuat adegan ke dalam buku
% catatan Euler.
% 
% Selain pov3d(), ada banyak fungsi, yang menghasilkan objek Povray.
% Fungsi ini mengembalikan string, berisi kode Povray untuk objek. Untuk
% menggunakan fungsi ini, mulai file Povray dengan povstart(). Kemudian
% gunakan writeln(...) untuk menulis objek ke file adegan. Akhirnya,
% akhiri file dengan povend(). Secara default, raytracer akan mulai, dan
% PNG akan dimasukkan ke dalam notebook Euler.
% 
% Fungsi objek memiliki parameter yang disebut, yang membutuhkan string
% dengan kode Povray untuk tekstur dan akhir objek. Fungsi povlook()
% dapat digunakan untuk menghasilkan string ini. Ini memiliki parameter
% untuk warna, transparansi, Phong Shading dll.
% 
% Perhatikan bahwa seluruh bidang Povray memiliki sistem koordinat lain.
% Antarmuka ini menerjemahkan semua koordinat ke sistem Povray. Jadi
% Anda dapat terus berpikir dalam sistem koordinat Euler dengan z
% menunjuk vertikal ke atas, dan sumbu x,y,z dalam arti tangan kanan.
%% Anda harus memuat file povray.
>load povray;
% Pastikan, direktori Povray bin ada di jalurnya. Jika tidak, edit
% variabel berikut sehingga berisi jalur ke povray yang dapat
% dieksekusi.
>defaultpovray="C:\Program Files\POV-Ray\v3.7\bin\pvengine.exe"
C:\Program Files\POV-Ray\v3.7\bin\pvengine.exe
% Untuk kesan pertama, kita membuat plot dari sebuah fungsi sederhana.
% Perintah berikut menghasilkan sebuah file povray di direktori pengguna
% Anda, dan menjalankan Povray untuk melakukan ray tracing terhadap file
% ini.
% 
% Jika Anda menjalankan perintah berikut, GUI Povray seharusnya akan
% terbuka, menjalankan file, dan secara otomatis menutupnya. Karena
% alasan keamanan, Anda akan ditanya apakah Anda ingin mengizinkan file
% exe ini untuk berjalan. Anda dapat menekan batal untuk menghentikan
% pertanyaan lebih lanjut. Anda mungkin perlu menekan OK di jendela
% Povray untuk mengakui dialog awal Povray.
>plot3d("x^2+y^2",zoom=2):
 %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-066.png
>pov3d("x^2+y^2",zoom=3);
 %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-067.png
% Kita dapat membuat fungsinya transparan dan menambahkan penyelesaian
% lainnya. Kita juga dapat menambahkan garis level ke plot fungsi.
>pov3d("x^2+y^3",axiscolor=red,angle=-45°,>anaglyph, ...
>  look=povlook(cyan,0.2),level=-1:0.5:1,zoom=3.8);
 %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-068.png
% Terkadang perlu untuk mencegah penskalaan fungsi, dan menskalakan
% fungsi secara manual.
% 
% Kita memplot himpunan titik pada bidang kompleks, dimana hasil kali
% jarak ke 1 dan -1 sama dengan 1.
>pov3d("((x-1)^2+y^2)*((x+1)^2+y^2)/40",r=2, ...
>  angle=-120°,level=1/40,dlevel=0.005,light=[-1,1,1],height=10°,n=50, ...
>  <fscale,zoom=3.8);
 %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-069.png
% * Plotting dengan Koordinat
% 
% Daripada menggunakan fungsi, kita dapat melakukan plotting dengan
% menggunakan koordinat. Seperti pada plot3d, kita memerlukan tiga
% matriks untuk mendefinisikan objek.
% 
% Pada contoh ini, kita memutar sebuah fungsi sekitar sumbu z.
>
>function f(x) := x^3-x+1; ...
>x=-1:0.01:1; t=linspace(0,2pi,50)'; ...
>Z=x; X=cos(t)*f(x); Y=sin(t)*f(x); ...
>pov3d(X,Y,Z,angle=40°,look=povlook(red,0.1),height=50°,axis=0,zoom=4,light=[10,5,15]);
 %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-070.png
% Dalam contoh berikut, kita memplot gelombang teredam. Kita
% menghasilkan gelombang tersebut dengan bahasa matriks Euler.
% 
% Kita juga menunjukkan bagaimana objek tambahan dapat ditambahkan ke
% dalam sebuah adegan pov3d. Untuk menghasilkan objek-objek tersebut,
% lihat contoh-contoh berikutnya. Perlu diperhatikan bahwa plot3d
% mengubah skala plot sehingga cocok dalam kubus satuan.
>r=linspace(0,1,80); phi=linspace(0,2pi,80)'; ...
>x=r*cos(phi); y=r*sin(phi); z=exp(-5*r)*cos(8*pi*r)/3;  ...
>pov3d(x,y,z,zoom=6,axis=0,height=30°,add=povsphere([0.5,0,0.25],0.15,povlook(red)), ...
>  w=500,h=300);
 %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-071.png
% Dengan metode shading canggih Povray, hanya sedikit titik dapat
% menghasilkan permukaan yang sangat halus. Hanya di batas-batas dan
% dalam bayangan trik ini mungkin menjadi jelas.
% 
% Untuk ini, kita perlu menambahkan vektor normal di setiap titik
% matriks.
>Z &= x^2*y^3

                                 2  3
                                x  y

% Persamaan permukaannya adalah [x,y,Z]. Kami menghitung dua turunan
% dari x dan y dan mengambil perkalian silangnya sebagai normal.
>dx &= diff([x,y,Z],x); dy &= diff([x,y,Z],y);
% Kami mendefinisikan normal sebagai produk silang dari turunan ini, dan
% mendefinisikan fungsi koordinat.
>N &= crossproduct(dx,dy); NX &= N[1]; NY &= N[2]; NZ &= N[3]; N,

                               3       2  2
                       [- 2 x y , - 3 x  y , 1]

% Kami hanya menggunakan 25 poin.
>x=-1:0.5:1; y=x';
>pov3d(x,y,Z(x,y),angle=10°, ...
>  xv=NX(x,y),yv=NY(x,y),zv=NZ(x,y),<shadow);
 %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-072.png
% Berikut adalah simpul Trefoil yang dibuat oleh A. Busser dalam Povray.
% Terdapat versi yang ditingkatkan dari ini dalam contoh-contoh.
% 
% See: Examples\Trefoil Knot | Trefoil Knot
% 
% Untuk tampilan yang baik dengan tidak terlalu banyak titik, kami
% menambahkan vektor normal di sini. Kami menggunakan Maxima untuk
% menghitung vektor normal untuk kami. Pertama, tiga fungsi koordinat
% sebagai ekspresi simbolis.
>X &= ((4+sin(3*y))+cos(x))*cos(2*y); ...
>Y &= ((4+sin(3*y))+cos(x))*sin(2*y); ...
>Z &= sin(x)+2*cos(3*y);
% Kemudian kedua vektor turunan ke x dan y.
>dx &= diff([X,Y,Z],x); dy &= diff([X,Y,Z],y);
% Sekarang normalnya, yaitu perkalian silang kedua turunannya.
>dn &= crossproduct(dx,dy);
% Kita sekarang mengevaluasi semua ini secara numerik.
>x:=linspace(-%pi,%pi,40); y:=linspace(-%pi,%pi,100)';
% Vektor normal adalah evaluasi ekspresi simbolik dn[i] untuk i=1,2,3.
% Sintaksnya adalah &"expression"(parameter). Ini adalah alternatif dari
% metode pada contoh sebelumnya, di mana kita mendefinisikan ekspresi
% simbolik NX, NY, NZ terlebih dahulu.
>pov3d(X(x,y),Y(x,y),Z(x,y),>anaglyph,axis=0,zoom=5,w=450,h=350, ...
>  <shadow,look=povlook(blue), ...
>  xv=&"dn[1]"(x,y), yv=&"dn[2]"(x,y), zv=&"dn[3]"(x,y));
 %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-073.png
% Kita juga dapat menghasilkan grid dalam 3D.
>povstart(zoom=4); ...
>x=-1:0.5:1; r=1-(x+1)^2/6; ...
>t=(0°:30°:360°)'; y=r*cos(t); z=r*sin(t); ...
>writeln(povgrid(x,y,z,d=0.02,dballs=0.05)); ...
>povend();
 %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-074.png
% Dengan povgrid(), kurva dimungkinkan.
>povstart(center=[0,0,1],zoom=3.6); ...
>t=linspace(0,2,1000); r=exp(-t); ...
>x=cos(2*pi*10*t)*r; y=sin(2*pi*10*t)*r; z=t; ...
>writeln(povgrid(x,y,z,povlook(red))); ...
>writeAxis(0,2,axis=3); ...
>povend();
 %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-075.png
% * Objek Povray
% 
% Di atas, kita menggunakan pov3d untuk memplot permukaan. Antarmuka
% povray di Euler juga dapat menghasilkan objek Povray. Objek ini
% disimpan sebagai string di Euler, dan perlu ditulis ke file Povray.
% 
% Kita memulai output dengan povstart().
>povstart(zoom=4);
% Pertama kita mendefinisikan tiga silinder, dan menyimpannya dalam
% string di Euler.
% 
% Fungsi povx() dll. hanya mengembalikan vektor [1,0,0], yang dapat
% digunakan sebagai gantinya.
>c1=povcylinder(-povx,povx,1,povlook(red)); ...
>c2=povcylinder(-povy,povy,1,povlook(yellow)); ...
>c3=povcylinder(-povz,povz,1,povlook(blue)); ...
% String tersebut berisi kode Povray, yang tidak perlu kita pahami pada
% saat itu.
>c2
cylinder { <0,0,-1>, <0,0,1>, 1
 texture { pigment { color rgb <0.941176,0.941176,0.392157> }  } 
 finish { ambient 0.2 } 
 }
% Seperti yang Anda lihat, kita menambahkan tekstur pada objek dalam
% tiga warna berbeda.
% 
% Hal ini dilakukan oleh povlook(), yang mengembalikan string dengan
% kode Povray yang relevan. Kita dapat menggunakan warna default Euler,
% atau menentukan warna kita sendiri. Kita juga dapat menambahkan
% transparansi, atau mengubah cahaya sekitar.
>povlook(rgb(0.1,0.2,0.3),0.1,0.5)
 texture { pigment { color rgbf <0.101961,0.2,0.301961,0.1> }  } 
 finish { ambient 0.5 } 

% Sekarang kita mendefinisikan objek persimpangan, dan menulis hasilnya
% ke file.
>writeln(povintersection([c1,c2,c3]));
% Persimpangan tiga silinder sulit untuk divisualisasikan jika Anda
% belum pernah melihatnya sebelumnya.
>povend;
 %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-076.png
% Fungsi berikut menghasilkan fraktal secara rekursif.
% 
% Fungsi pertama menunjukkan bagaimana Euler menangani objek Povray
% sederhana. Fungsi povbox() mengembalikan string, yang berisi koordinat
% kotak, tekstur, dan hasil akhir.
>function onebox(x,y,z,d) := povbox([x,y,z],[x+d,y+d,z+d],povlook());
>function fractal (x,y,z,h,n) ...
$ if n==1 then writeln(onebox(x,y,z,h));
$ else
$   h=h/3;
$   fractal(x,y,z,h,n-1);
$   fractal(x+2*h,y,z,h,n-1);
$   fractal(x,y+2*h,z,h,n-1);
$   fractal(x,y,z+2*h,h,n-1);
$   fractal(x+2*h,y+2*h,z,h,n-1);
$   fractal(x+2*h,y,z+2*h,h,n-1);
$   fractal(x,y+2*h,z+2*h,h,n-1);
$   fractal(x+2*h,y+2*h,z+2*h,h,n-1);
$   fractal(x+h,y+h,z+h,h,n-1);
$ endif;
$endfunction
>povstart(fade=10,<shadow);
>fractal(-1,-1,-1,2,4);
>povend();
 %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-077.png
% Perbedaan memungkinkan pemisahan satu objek dari objek lainnya.
% Seperti persimpangan, ada bagian dari objek CSG di Povray.
>povstart(light=[5,-5,5],fade=10);
% Untuk demonstrasi ini, kita mendefinisikan sebuah objek dalam Povray,
% daripada menggunakan sebuah string dalam Euler. Definisi ditulis ke
% file secara langsung.
% 
% Koordinat sebuah kotak -1 hanya berarti [-1,-1,-1].
>povdefine("mycube",povbox(-1,1));
% Kita bisa menggunakan objek ini di povobject(), yang mengembalikan
% string seperti biasa.
>c1=povobject("mycube",povlook(red));
% Kami membuat kubus kedua, dan memutar serta menskalakannya sedikit.
>c2=povobject("mycube",povlook(yellow),translate=[1,1,1], ...
>  rotate=xrotate(10°)+yrotate(10°), scale=1.2);
% Lalu kita ambil selisih kedua benda tersebut.
>writeln(povdifference(c1,c2));
% Sekarang tambahkan tiga sumbu.
>writeAxis(-1.2,1.2,axis=1); ...
>writeAxis(-1.2,1.2,axis=2); ...
>writeAxis(-1.2,1.2,axis=4); ...
>povend();
 %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-078.png
% * Fungsi Implisit
% 
% Povray dapat memplot himpunan di mana f(x,y,z)=0, seperti parameter
% implisit di plot3d. Namun hasilnya terlihat jauh lebih baik.
% 
% Sintaks untuk fungsinya sedikit berbeda. Anda tidak dapat menggunakan
% keluaran ekspresi Maxima atau Euler.
% 
% latex: ((x^2+y^2-c^2)^2+(z^2-1)^2)*((y^2+z^2-c^2)^2+(x^2-1)^2)*((z^2+x^2-c^2)^2+(y^2-1)^2)=d
% %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-079.png
% // latex: ((x^2+y^2-c^2)^2+(z^2-1)^2)*((y^2+z^2-c^2)^2+(x^2-1)^2)*((z^2+x^2-c^2)^2+(y^2-1)^2)=d
>
>povstart(angle=70°,height=50°,zoom=4);
>c=0.1; d=0.1; ...
>writeln(povsurface("(pow(pow(x,2)+pow(y,2)-pow(c,2),2)+pow(pow(z,2)-1,2))*(pow(pow(y,2)+pow(z,2)-pow(c,2),2)+pow(pow(x,2)-1,2))*(pow(pow(z,2)+pow(x,2)-pow(c,2),2)+pow(pow(y,2)-1,2))-d",povlook(red))); ...
>povend();
object {
isosurface {
function { (pow(pow(x,2)+pow(y,2)-pow(c,2),2)+pow(pow(z,2)-1,2))*(pow(pow(y,2)+pow(z,2)-pow(c,2),2)+pow(pow(x,2)-1,2))*(pow(pow(z,2)+pow(x,2)-pow(c,2),2)+pow(pow(y,2)-1,2))-d }
max_gradient 5
open
contained_by { box { <-1,-1,-1>, <1,1,1>
 } }
 texture { pigment { color rgb <0.564706,0.0627451,0.0627451> }  } 
 finish { ambient 0.2 } 
}}
 %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-080.png
>povstart(angle=25°,height=10°); 
>writeln(povsurface("pow(x,2)+pow(y,2)*pow(z,2)-1",povlook(blue),povbox(-2,2,"")));
>povend();
 %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-081.png
>
>povstart(angle=70°,height=50°,zoom=4);
% Buat permukaan implisit. Perhatikan sintaksis yang berbeda dalam
% ekspresi.
>writeln(povsurface("pow(x,2)*y-pow(y,3)-pow(z,2)",povlook(green))); ...
>writeAxes(); ...
>povend();
 %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-082.png
% * Objek Mesh
% 
% Dalam contoh ini, kita akan menunjukkan bagaimana cara membuat objek
% mesh, dan menggambarkannya dengan informasi tambahan.
% 
% Kita ingin memaksimalkan xy dengan syarat x+y=1 dan mendemonstrasikan
% sentuhan tangensial dari garis level.
>povstart(angle=-10°,center=[0.5,0.5,0.5],zoom=7);
% Kita tidak dapat menyimpan objek dalam bentuk string seperti
% sebelumnya, karena terlalu besar. Jadi, kita mendefinisikan objek
% dalam file Povray menggunakan #declare. Fungsi povtriangle()
% melakukannya secara otomatis. Ini dapat menerima vektor normal seperti
% pov3d().
% 
% Berikut ini mendefinisikan objek jaringan (mesh), dan langsung
% menulisnya ke dalam file.
>x=0:0.02:1; y=x'; z=x*y; vx=-y; vy=-x; vz=1;
>mesh=povtriangles(x,y,z,"",vx,vy,vz);
% Sekarang kita mendefinisikan dua cakram, yang akan berpotongan dengan
% permukaan.
>cl=povdisc([0.5,0.5,0],[1,1,0],2); ...
>ll=povdisc([0,0,1/4],[0,0,1],2);
% Tulis permukaannya dikurangi kedua cakram.
>writeln(povdifference(mesh,povunion([cl,ll]),povlook(green)));
% Tuliskan kedua perpotongan tersebut.
>writeln(povintersection([mesh,cl],povlook(red))); ...
>writeln(povintersection([mesh,ll],povlook(gray)));
% Tulis poin maksimal.
>writeln(povpoint([1/2,1/2,1/4],povlook(gray),size=2*defaultpointsize));
% Tambahkan sumbu dan selesai.
>writeAxes(0,1,0,1,0,1,d=0.015); ...
>povend();
 %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-083.png
% * Anaglif dalam Povray
% 
% Untuk menghasilkan anaglif untuk kacamata merah/cyan, Povray harus
% dijalankan dua kali dari posisi kamera yang berbeda. Ini menghasilkan
% dua file Povray dan dua file PNG, yang dimuat dengan fungsi
% loadanaglyph().
% 
% Tentu saja, Anda memerlukan kacamata merah/cyan untuk melihat
% contoh-contoh berikut dengan benar.
% 
% Fungsi pov3d() memiliki sakelar sederhana untuk menghasilkan anaglif.
>pov3d("-exp(-x^2-y^2)/2",r=2,height=45°,>anaglyph, ...
>  center=[0,0,0.5],zoom=3.5);
 %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-084.png
% Jika Anda membuat adegan dengan objek, Anda perlu memasukkan pembuatan
% adegan ke dalam fungsi, dan menjalankannya dua kali dengan nilai
% berbeda untuk parameter anaglyph.
>function myscene ...
$  s=povsphere(povc,1);
$  cl=povcylinder(-povz,povz,0.5);
$  clx=povobject(cl,rotate=xrotate(90°));
$  cly=povobject(cl,rotate=yrotate(90°));
$  c=povbox([-1,-1,0],1);
$  un=povunion([cl,clx,cly,c]);
$  obj=povdifference(s,un,povlook(red));
$  writeln(obj);
$  writeAxes();
$endfunction
% Fungsi povanaglyph() melakukan semua ini. Parameternya seperti
% gabungan povstart() dan povend().
>povanaglyph("myscene",zoom=4.5);
 %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-085.png
% * Mendefinisikan Objek sendiri
% 
% Antarmuka povray Euler berisi banyak objek. Namun Anda tidak dibatasi
% pada hal ini. Anda dapat membuat objek sendiri, yang menggabungkan
% objek lain, atau merupakan objek yang benar-benar baru.
% 
% Kita mendemonstrasikan torus. Perintah Povray untuk ini adalah
% "torus". Jadi kita mengembalikan string dengan perintah ini dan
% parameternya. Perhatikan bahwa torus selalu berpusat pada titik asal.
>function povdonat (r1,r2,look="") ...
$  return "torus {"+r1+","+r2+look+"}";
$endfunction
% Ini torus pertama kita.
>t1=povdonat(0.8,0.2)
torus {0.8,0.2}
% Mari kita gunakan objek ini untuk membuat torus kedua, diterjemahkan
% dan diputar.
>t2=povobject(t1,rotate=xrotate(90°),translate=[0.8,0,0])
object { torus {0.8,0.2}
 rotate 90 *x 
 translate <0.8,0,0>
 }
% Sekarang kita tempatkan objek-objek tersebut ke dalam sebuah adegan.
% Untuk tampilannya kami menggunakan Phong Shading.
>povstart(center=[0.4,0,0],angle=0°,zoom=3.8,aspect=1.5); ...
>writeln(povobject(t1,povlook(green,phong=1))); ...
>writeln(povobject(t2,povlook(green,phong=1))); ...
%  >povend();
% 
% memanggil program Povray. Namun, jika terjadi kesalahan, program ini
% tidak akan menampilkan pesan kesalahan. Oleh karena itu, Anda
% sebaiknya menggunakan
% 
%  >povend(<exit);
% 
% jika ada yang tidak berfungsi. Ini akan membuat jendela Povray tetap
% terbuka.
>povend(h=320,w=480);
 %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-086.png
% Berikut adalah contoh yang lebih rinci. Kita menyelesaikan
% 
% latex: Ax \le b, \quad x \ge 0, \quad c.x \to \text{Max.}
% %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-087.png
% // latex: Ax \le b, \quad x \ge 0, \quad c.x \to \text{Max.}
% 
% dan menunjukkan titik-titik yang memungkinkan dan optimum dalam plot
% 3D.
>A=[10,8,4;5,6,8;6,3,2;9,5,6];
>b=[10,10,10,10]';
>c=[1,1,1];
% Pertama, mari kita periksa, apakah contoh ini punya solusinya.
>x=simplex(A,b,c,>max,>check)'
[0,  1,  0.5]
% Ya, itu sudah ada.
% 
% Selanjutnya, kita mendefinisikan dua objek. Yang pertama adalah bidang
% 
% latex: a \cdot x \le b
% %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-088.png
% // latex: a \cdot x \le b
>function oneplane (a,b,look="") ...
$  return povplane(a,b,look)
$endfunction
% Kemudian kita mendefinisikan perpotongan semua setengah ruang dan
% sebuah kubus.
>function adm (A, b, r, look="") ...
$  ol=[];
$  loop 1 to rows(A); ol=ol|oneplane(A[#],b[#]); end;
$  ol=ol|povbox([0,0,0],[r,r,r]);
$  return povintersection(ol,look);
$endfunction
% Sekarang kita dapat merencanakan adegannya.
>povstart(angle=120°,center=[0.5,0.5,0.5],zoom=3.5); ...
>writeln(adm(A,b,2,povlook(green,0.4))); ...
>writeAxes(0,1.3,0,1.6,0,1.5); ...
% Berikut ini adalah lingkaran di sekitar optimal.
>writeln(povintersection([povsphere(x,0.5),povplane(c,c.x')], ...
>  povlook(red,0.9)));
% Dan kesalahan ke arah optimal.
>writeln(povarrow(x,c*0.5,povlook(red)));
% Kita menambahkan teks ke layar. Teks hanyalah objek 3D. Kita perlu
% menempatkan dan memutarnya sesuai dengan pandangan kita.
>writeln(povtext("Linear Problem",[0,0.2,1.3],size=0.05,rotate=5°)); ...
>povend();
 %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-089.png
% * Contoh lain
% 
% Anda dapat menemukan beberapa contoh lebih lanjut untuk Povray di
% Euler dalam file-file berikut.
% 
% See: Examples/Dandelin Spheres
%% See: Examples/Donat Math
%% See: Examples/Trefoil Knot
%% See: Examples/Optimization by Affine Scaling
% 
% * Latihan Soal
% 
% 1.Gambarlah grafik dari fungsi berikut.
%% latex: f(x,y)=x^2+3y^2
% %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-090.png
% // latex: f(x,y)=x^2+3y^2
%% Jawab :
>plot3d("x^2+3*y^2",n=40,grid=2):
 %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-091.png
% 2. Gambarlah grafik dari fungsi berikut
%% latex: f(x,y)= (2x^2+y^2)e^{x^2-y^2}
% %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-092.png
% // latex: f(x,y)= (2x^2+y^2)e^{x^2-y^2}
%% Jawab :
>plot3d("(2*x^2+y^2)*E^(x^2-y^2)",scale={1,2},xmin=-5,xmax=5,ymin=-7,ymax=7,frame=4):
 %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-093.png
% 3. Gambarkan grafik fungsi logaritma berikut
%% latex: f(x,y)=log(x^2+2y^2)
% %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-094.png
% // latex: f(x,y)=log(x^2+2y^2)
%% Jawab :
>plot3d("log(x^2+2*y^2)"):
 %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-095.png
% 4. Gambarkan grafik fungsi dari
%% latex: g(x,y)=sin(2x).cos(2y)
% %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-096.png
% // latex: g(x,y)=sin(2x).cos(2y)
%% Jawab :
>plot3d("sin(2*x)*cos(2*y)"):
 %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-097.png
% 5. Bagaimana bentuk atau pola dari objek persamaan berikut
% 
% latex: f(x,y) = cos(x)sin(y)
% %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-098.png
% // latex: f(x,y) = cos(x)sin(y)
%% latex: g(x,y) = sin(x)cos(y)
% %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-099.png
% // latex: g(x,y) = sin(x)cos(y)
%% latex: h(x,y) = cos(x)
% %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-100.png
% // latex: h(x,y) = cos(x)
%% dengan
%% latex: 0 \leq x \leq 2\pi
% %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-101.png
% // latex: 0 \leq x \leq 2\pi
% 
% latex: -\frac{\pi}{2} \leq y \leq \frac{\pi}{2}
% %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-102.png
% // latex: -\frac{\pi}{2} \leq y \leq \frac{\pi}{2}
%% Jawab :
>plot3d("cos(x)*sin(y)","sin(x)*cos(y)","cos(x)", a=0, b=2*pi, c=pi/2, d=-pi/2, ...
>>hue,color=red, light=[0,1,0],<frame, ...
>n=90, grid=[15,30],wirecolor=green,zoom=3):
 %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-103.png
% 6. Gambarkan fungsi dari
%% latex: A=\{(x,y,z):x^3+2y^3+3z^3=1\}
% %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-104.png
% // latex: A=\{(x,y,z):x^3+2y^3+3z^3=1\}
%% Jawab :
>plot3d("x^3+2*y^3+3*z^3-1",r=5,implicit=3):
 %image% EMT4Plot3D_Ghifa Attaya Ulhaq_22305144038-105.png
>
>
